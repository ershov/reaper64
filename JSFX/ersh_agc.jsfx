/**
  JSFX Name: AGC - Automatic Gain Control
  Author: ERSH, Yury Ershov
  Licence: LGPL
  REAPER: 6.0
  Version: 1.3.0
  Tags: analysis loudness dynamics
  Provides:
    ersh_agc-inc/ersh_alloc.jsfx-inc
    ersh_agc-inc/ersh_deque.jsfx-inc
    ersh_agc-inc/ersh_graphlib.jsfx-inc
    ersh_agc-inc/ersh_ringbuf_abs.jsfx-inc
    ersh_agc.jsfx.rpl
  About:
    Automatic Gain Control (AGC) by ERSH, based on Loudness Meter Peak/RMS/LUFS (Cockos).
    Auto gain control (rider) by Peak, True Peak, RMS, LUFS.

    Plugin uses lookahead window to watch for maximum gain in the signal and tries to keep the output gain below the specified level.

    If the input gain in not a real/true peak then the output can actually exceed 0dB level.
    For that case there is a peak limiter that use the same lookahead window and engages shortly before the prospective peak excess and compensates for that event.

    PDC Delay setting is to request plugin to report the lookahead window to Reaper so that the delay is automatically compensated.
    That however incurs additional CPU and Memory usage, that's why there is a setting to turn it on/off.

    Some settings are adjustable on the fly, some are not. Make sure to stop/start the playback to apply the changes.
  Screenshot: https://i.imgur.com/u10GzPp.png
  Link: https://github.com/ershov/reaper64
  Link: https://youtu.be/hPcTy4PMVwg
  Changelog:
    v1.0:
    * Initial release
    v1.1.0:
    * Add Predictive Peak Limiter for non-peak driven modes
    v1.2.0:
    * (internal) Use separate files for libraries
    v1.2.1:
    * Add presets
    v1.2.2:
    * Correct initial gain value
    v1.2.4:
    * Correct "long" modes (RMS, LUFS) lookahead.
    v1.2.5:
    * Add "Reset" button
    v1.3.0:
    * Improvements to peak limiter
    * Added a "Limiter" preset
*/

// (C) 2021, Yury Ershov.
// This effect Copyright (C) 2021 and later Cockos Incorporated
// License: LGPL - http://www.gnu.org/licenses/lgpl.html

import ersh_agc-inc/ersh_alloc.jsfx-inc
import ersh_agc-inc/ersh_deque.jsfx-inc
import ersh_agc-inc/ersh_graphlib.jsfx-inc
import ersh_agc-inc/ersh_ringbuf_abs.jsfx-inc

desc: AGC - Automatic Gain Control
//tags: analysis loudness dynamics
//author: ERSH

preset:"Normal" 4 -18 5 1 -50 50 1 1
preset:"Medium" 4 -18 3 4 -50 50 1 1
preset:"Fast" 4 -18 1 10 -50 50 1 1
preset:"Extreme" 0 -10 0.1 100 -100 100 1 1
preset:"Limiter" 0 0 0.05 1000 -50 0 0 1

slider1:agc_by=4<0,7,1{Peak,True Peak,RMS-M,RMS-I,LUFS-M,LUFS-S,LUFS-I,LUFS-LRA}>AGC input
slider2:agc_to=-18<-100,0,0.01>AGC Output level (in units above)
slider3:agc_lookahead=5<0,30,0.001>AGC Lookahead, sec
slider4:agc_release_rate=1<1,20,0.001>AGC Release, dB/sec
slider5:agc_min=-50<-100,100,0.01>AGC Minimum Gain
slider6:agc_max=50<-100,100,0.01>AGC Maximum Gain
slider7:cfg_use_peak_limiter=1<0,1,1{No,Yes}>Use Predictive Peak Limiter
slider8:use_pdc_delay=1<0,1,1{No,Yes}>Set PDC Delay

slider19:cfg_mono=0<0,1,1{off,on}>Force mono analysis
slider20:cfg_out_auto=0<0,16,1{off,all,all (inverted),Peak,Peak (inverted),RMS-M,RMS-M (inverted),RMS-I,RMS-I (inverted),LUFS-M,LUFS-M (inverted),LUFS-S,LUFS-S (inverted),LUFS-I,LUFS-I (inverted),LRA,LRA (inverted)}>Output loudness values as automation

slider30:outparm_peak=-150<-150,20,1>-Peak/True peak dB (output)
slider31:outparm_rms_m=-100<-100,0,1>-RMS-M (output)
slider32:outparm_rms_i=-100<-100,0,1>-RMS-I (output)
slider33:outparm_lufs_m=-100<-100,0,1>-LUFS-M (output)
slider34:outparm_lufs_s=-100<-100,0,1>-LUFS-S (output)
slider35:outparm_lufs_i=-100<-100,0,1>-LUFS-I (output)
slider36:outparm_lufs_lra=0<0,100,1>-LRA (output)
slider37:outparm_agc_gain=-100<-100,0,1>-AGC Gain (output)
slider38:outparm_agc_winmax=-100<-100,0,1>-AGC Window Max Gain (output)
slider39:outparm_agc_envelope=-100<-100,0,1>-AGC Envelope (output)

// TODO: Add side chain input(s)
// TODO: Output some slider values as pin outs
// TODO: Need to maintain more than 2 inputs?

//options:no_meter

// TODO: options:maxmem=32000000 // __memtop()

@init

PEAK_DEBUG=0;

INF=150;
_INF=-INF;
LOG10 = log(10);

alloc.__memtop = __memtop();

cfg_reinit=1; // Reset on playback start

// rms-i, lufs-i, lra calculated 0=during playback only, 1=always
WANT_INTEGRATED_ALWAYS=0;

FONT_SZ_MIN=12;
FONT_SZ_MAX=16;
// $xRRGGBB
BG_COLOR=$x000000;
GRID_COLOR=$x7f7f7f;
TEXT_COLOR=$xffffff;
PEAK_COLOR=$x3fff3f;
PEAK_CLIP_COLOR=$xff0000;
RMS_COLOR=$x7f7f7f;
LUFS_COLOR=$x00bfff;
LUFS_HIST_COLOR=$x005f7f;
RED_COLOR=$xff0000;
YELLOW_COLOR=$xffff00;
MONO_COLOR=$xffa500;

PEAK_METER_DECAY=0.150;
LOUD_METER_UPDATE=0.100; // default 100ms, must be divisible into both 0.4 and 3.0
LOUD_METER_SPEED=0.075;

UI_SLIDER_MASK=(1<<22)-1;

ext_noinit=1;
ext_nodenorm=1;
gfx_ext_retina=max(gfx_ext_retina,1);

sliders_showing=1;

NUM_BINS=1024;
BINS_PER_DB=10;
DB_PER_BIN=1/BINS_PER_DB;

REAPER_MAX_CHANNELS=64;

// sinc filter for true peak
function sinc_gen_val() global(srate) local(sincpos windowpos) instance(slice_pos) (
  windowpos = (2.0 * $pi / 16/*SINC_FILTER_SIZE*/) * slice_pos;
  sincpos = $pi * (slice_pos - 16/*SINC_FILTER_SIZE*/ * .5);
  slice_pos += 1;
  (0.53836 - cos(windowpos)*0.46164) * sin(sincpos) / sincpos;
);

function sinc_gen_slice(cs, o*) instance(sinc_gen_val slice_pos) global() (
  slice_pos = cs;
  o.v00 = sinc_gen_val(); o.v01 = sinc_gen_val(); o.v02 = sinc_gen_val(); o.v03 = sinc_gen_val();
  o.v04 = sinc_gen_val(); o.v05 = sinc_gen_val(); o.v06 = sinc_gen_val(); o.v07 = sinc_gen_val();
  o.v08 = sinc_gen_val(); o.v09 = sinc_gen_val(); o.v10 = sinc_gen_val(); o.v11 = sinc_gen_val();
  o.v12 = sinc_gen_val(); o.v13 = sinc_gen_val(); o.v14 = sinc_gen_val(); o.v15 = sinc_gen_val();
);

function sinc_init() global(srate) instance(sinc_gen_slice) (
  sinc_gen_slice(srate < 96000 ? .25 : .5,this.s1);
  srate < 96000 ? (
    sinc_gen_slice(.5, this.s2);
    sinc_gen_slice(.75, this.s3);
  );
);

function sinc_slice_abs(hist*) global() (
  abs(
    hist.h00 * this.v00 + hist.h01 * this.v01 + hist.h02 * this.v02 + hist.h03 * this.v03 +
    hist.h04 * this.v04 + hist.h05 * this.v05 + hist.h06 * this.v06 + hist.h07 * this.v07 +
    hist.h08 * this.v08 + hist.h09 * this.v09 + hist.h10 * this.v10 + hist.h11 * this.v11 +
    hist.h12 * this.v12 + hist.h13 * this.v13 + hist.h14 * this.v14 + hist.h15 * this.v15
  );
);

function init(chidx)
(
  this.chan=chidx;
  this.wt = chidx < 3 || num_ch < 6 ? 1 : chidx == 3 ? 0 : sqrt(2);
  this.f1p1=this.f1p2=this.f2p1=this.f2p2=0;
  this.pkval=this.hipkval=this.clips=0;
  this.ch_lufs_sum=this.ch_rms_sum=0;

  this.h00 = this.h01 = this.h02 = this.h03 = this.h04 = this.h05 = this.h06 = this.h07 =
  this.h08 = this.h09 = this.h10 = this.h11 = this.h12 = this.h13 = this.h14 = this.h15 = 0;
);

function decay()
(
  this.pkval *= pk_decay;
);

function copypks() global(pk hipk clip_cnt) (
  pk[this.chan] = this.pkval;
  hipk[this.chan] = this.hipkval;
  clip_cnt[this.chan] = this.clips;
);

function proc(lspl)
  local(pspl f1p0 f2p0)
  instance(wt ch_rms_sum ch_lufs_sum pkval hipkval clips
           f1p1 f1p2 f2p1 f2p2)
  global(f1a1 f1a2 f1b0 f1b1 f1b2
         f2a1 f2a2 f2b0 f2b1 f2b2
         srate agc_by
         sinc.s1.sinc_slice_abs
         sinc.s2.sinc_slice_abs
         sinc.s3.sinc_slice_abs
         lval rval win_pos global_peak global_sample_peak)
(
  agc_by == 1 ? (
    // True Peak
    this.h15 = this.h14; this.h14 = this.h13; this.h13 = this.h12; this.h12 = this.h11;
    this.h11 = this.h10; this.h10 = this.h09; this.h09 = this.h08; this.h08 = this.h07;
    this.h07 = this.h06; this.h06 = this.h05; this.h05 = this.h04; this.h04 = this.h03;
    this.h03 = this.h02; this.h02 = this.h01; this.h01 = this.h00; this.h00 = lspl;
  
    pspl = max(abs(this.h08), sinc.s1.sinc_slice_abs(this));
    srate < 96000 ? pspl = max(pspl,max(sinc.s2.sinc_slice_abs(this), sinc.s3.sinc_slice_abs(this)));
  ) : (
    // Peak
    pspl=abs(lspl);
  );
  global_sample_peak = max(global_sample_peak, pspl);

  pspl > pkval ? (
    pkval=pspl;
    pspl > hipkval ? (
      hipkval=pspl;
      global_peak = max(global_peak,pspl);
    );
  );
  pspl > 1.0 ? clips += 1;

  win_pos == 0 ? ch_rms_sum=ch_lufs_sum=0;

  rval += (ch_rms_sum += lspl*lspl);
  
  lspl *= wt;

  f1p0=lspl-f1a1*f1p1-f1a2*f1p2;
  lspl=f1b0*f1p0+f1b1*f1p1+f1b2*f1p2;
  f1p2=f1p1;
  f1p1=f1p0;
  
  f2p0=lspl-f2a1*f2p1-f2a2*f2p2;
  lspl=f2b0*f2p0+f2b1*f2p1+f2b2*f2p2;
  f2p2=f2p1;
  f2p1=f2p0;

  lval += (ch_lufs_sum += lspl*lspl);
);

function init_lufs_filters()
  local(Vh Vb db f0 Q K a0)
  global(f1a1 f1a2 f1b0 f1b1 f1b2
         f2a1 f2a2 f2b0 f2b1 f2b2 srate)
(
  // f1,f2 could be combined into a 5th order filter

  db=3.999843853973347;
  f0=1681.974450955533;
  Q=0.7071752369554196;
  K=tan($pi*f0/srate);
  Vh=pow(10, db/20);
  Vb=pow(Vh, 0.4996667741545416);
  a0=1+K/Q+K*K;
  f1a1=2*(K*K-1)/a0;
  f1a2=(1-K/Q+K*K)/a0;
  f1b0=(Vh+Vb*K/Q+K*K)/a0;
  f1b1=2*(K*K-Vh)/a0;
  f1b2=(Vh-Vb*K/Q+K*K)/a0;

  f0=38.13547087602444;
  Q=0.5003270373238773;
  K=tan($pi*f0/srate);
  f2a1=2*(K*K-1)/(1+K/Q+K*K);
  f2a2=(1-K/Q+K*K)/(1+K/Q+K*K);
  f2b0=1;
  f2b1=-2;
  f2b2=1;
);

// Sliding max window // https://www.geeksforgeeks.org/sliding-window-maximum-maximum-of-all-subarrays-of-size-k/
function maxwin_init(sz) ( this.buf.rb_init(sz); this.dq.dq_init(sz); this.sz = sz; );
function maxwin_max() ( this.buf.rb_get_at(this.dq.dq_front()); );
function maxwin_off() ( this.dq.dq_front() - this.buf.head + this.sz; );
function maxwin_tail_gain() ( this.buf.rb_get_tail(); );
function maxwin_add(gain, cur_envelope_gain) local (winstart pos dpos gain_angle gain2) (
  // Remove the elements which are out of window
  winstart = this.buf.head - this.sz;
  while (!this.dq.dq_empty() && this.dq.dq_front() <= winstart) (
    this.dq.dq_pop_front();
  );

  // Check if the last element is smaller than the second last and not greater than the current
  gain2 = this.buf.rb_get_at(this.dq.dq_nback(0));
  this.dq.dq_size() > 1 &&
  gain2 < this.buf.rb_get_at(this.dq.dq_nback(1)) &&
  gain2 <= gain &&
  this.dq.dq_pop_back();

  // Remove all elements smaller than the currently being added element (remove useless elements)
  (cur_envelope_gain >= gain || this.maxwin_max() >= gain) ? (
    // If the slope is going down, just mark the peaks
    while (!this.dq.dq_empty() && gain >= this.buf.rb_get_at(this.dq.dq_back())) (
      this.dq.dq_pop_back();
    );
  ) : (
    // If the slope is going up, keep the closer but steeper edges
    gain_angle = (gain-cur_envelope_gain) / (this.sz+1);
    while (!this.dq.dq_empty() && (
              dpos = (pos=this.dq.dq_back())-winstart;
              gain2 = this.buf.rb_get_at(pos);
              //Keep the ones that are equal! vv
              (dpos > agc_pre) &&
              (gain_angle > (gain2 - cur_envelope_gain)/dpos))) (
      this.dq.dq_pop_back();
    );
    // If the slope is going up, mark both sides of "platos", skipping the middle
    agc_by >= 2 &&
    this.dq.dq_size() > 1 &&
    this.buf.rb_get_at(this.dq.dq_nback(0)) == gain &&
    this.buf.rb_get_at(this.dq.dq_nback(1)) == gain &&
    this.dq.dq_pop_back();
  );

  // This order matters! the second is using the updated "head"
  this.buf.rb_push(gain);
  this.dq.dq_push_back(this.buf.head);
);
/////////////////////////////////////////////////////////////////////////////
// up - fast, down - release
function mutate_gain_envelope(gain, target_gain, distance, pre, release_rate) (
  gain < target_gain ? (
    distance > pre ?
      (gain + (target_gain - gain) / (distance-pre+1)) :
      target_gain;
  ) :
    max(target_gain, (gain - release_rate/srate));
);
/////////////////////////////////////////////////////////////////////////////

function Reset()
(
  sinc.sinc_init();
  init_lufs_filters();

  alloc.top=0;
  pk=alloc(REAPER_MAX_CHANNELS);
  hipk=alloc(REAPER_MAX_CHANNELS);
  clip_cnt=alloc(REAPER_MAX_CHANNELS);

  m_win_sec=0.4;
  m_win_cnt=m_win_sec/LOUD_METER_UPDATE;
  s_win_sec=3;
  s_win_cnt=s_win_sec/LOUD_METER_UPDATE;

  win_pos=0;
  win_cnt=0;
  win_len=(LOUD_METER_UPDATE*srate)|0;
  i_win_len=1/(m_win_cnt*win_len);
  s_win_len=1/(s_win_cnt*win_len);

  rms_m_sum=0;
  rms_m_db=-INF;
  rms_m_db_max=-INF;
  rms_i_sum=0;
  rms_i_sum_cnt=0;
  rms_i_db=-INF;

  lufs_m_sum=0;
  lufs_m_sum_max=0;
  lufs_m_db=-INF;
  lufs_s_sum=0;
  lufs_s_sum_max=0;
  lufs_s_db=-INF;
  lufs_a_sum=0;
  lufs_a_sum_cnt=0;
  lufs_b_sum=0;
  lufs_b_sum_cnt=0;
  lra_db_diff = 0;
  lra_db_hi = lra_db_lo = lufs_i_db = lufs_m_db = lufs_s_db = _INF;

  th_lufs_i=th_lufs_s=th_lufs_m=th_rms_i=th_rms_m=0;

  cur_buf=0;
  cur_buf2=0;
  rms_buf=alloc(m_win_cnt);
  lufs_buf=alloc(m_win_cnt);
  lufs_buf2=alloc(s_win_cnt);
  lufs_a_hist=alloc(2*NUM_BINS);
  lufs_b_hist=alloc(NUM_BINS);
  db_hist=alloc(75);
  db_hist_max=0;

  global_peak = 0;

  ch0.init(0); ch1.init(1);
  num_ch > 2 ? ( ch2.init(2); ch3.init(3);
  num_ch > 4 ? ( ch4.init(4); ch5.init(5);
  num_ch > 6 ? ( ch6.init(6); ch7.init(7);
  num_ch > 8 ? ( ch8.init(8); ch9.init(9); ch10.init(10); ch11.init(11);
  num_ch > 12 ? ( ch12.init(12); ch13.init(13); ch14.init(14); ch15.init(15);
  num_ch > 16 ? ( ch16.init(16); ch17.init(17); ch18.init(18); ch19.init(19);
  num_ch > 20 ? ( ch20.init(20); ch21.init(21); ch22.init(22); ch23.init(23);
  num_ch > 24 ? ( ch24.init(24); ch25.init(25); ch26.init(26); ch27.init(27); ch28.init(28); ch29.init(29); ch30.init(30); ch31.init(31);
  num_ch > 32 ? ( ch32.init(32); ch33.init(33); ch34.init(34); ch35.init(35); ch36.init(36); ch37.init(37); ch38.init(38); ch39.init(39);
  num_ch > 40 ? ( ch40.init(40); ch41.init(41); ch42.init(42); ch43.init(43); ch44.init(44); ch45.init(45); ch46.init(46); ch47.init(47);
  num_ch > 48 ? ( ch48.init(48); ch49.init(49); ch50.init(50); ch51.init(51); ch52.init(52); ch53.init(53); ch54.init(54); ch55.init(55);
  num_ch > 56 ? ( ch56.init(56); ch57.init(57); ch58.init(58); ch59.init(59); ch60.init(60); ch61.init(61); ch62.init(62); ch63.init(63);
  ))))))))))));

  MAX_MODES = 20;
  //mode_delay_sec = alloc(MAX_MODES);
  mode_delay = alloc(MAX_MODES);
  mode_delay[0] = mode_delay[1] = 0;  // Peak
  mode_delay[2] = mode_delay[3] = mode_delay[4] = mode_delay[6] = ((m_win_cnt+0.1)*win_len/2)|0;
  mode_delay[5] = ((s_win_cnt+0.1)*win_len/2)|0;
  mode_delay[7] = 0;  // LRA

  use_peak_limiter = (agc_by > 1 && cfg_use_peak_limiter);
  agc_peak_release_db_sec = 1000;
  agc_peak_lookahead_sz = srate*0.05;
  agc_peak_pre = agc_peak_lookahead_sz/5;

  agc_lookahead_sz = max((agc_lookahead * srate)|0, mode_delay[agc_by]*2+4);
  use_peak_limiter ? agc_lookahead_sz = max(agc_lookahead_sz, agc_peak_lookahead_sz);
  pdc_delay = (use_pdc_delay ? agc_lookahead_sz : 0);
  agc_buf.maxwin_init(agc_lookahead_sz);
  use_peak_limiter ? (
    agc_pbuf_delay.rb_init(max(1, agc_lookahead_sz - agc_peak_lookahead_sz + 1));  // delay samples before putting them into peak's maxwin
    memset(agc_pbuf_delay.buf, _INF, agc_pbuf_delay.sz);
    agc_pbuf.maxwin_init(agc_peak_lookahead_sz);
  );
  spl0_buf.rb_init(agc_lookahead_sz);
  spl1_buf.rb_init(agc_lookahead_sz);
  agc_envelope_gain = agc_min;  // Never used anyway
  agc_output_gain = agc_max;           // Never used anyway
  agc_input = agc_input_prev = _INF;
  //agc_pre = agc_by < 2 ? srate/10 : srate*0.5;
  agc_pre = mode_delay[agc_by]+1;
  graph_range = max(abs(agc_min), abs(agc_max)) + abs(agc_to);

  g_init(500, 2*agc_lookahead_sz/srate);
  g_memstart = alloc.top;
  graph_smpl.g_add_graph("Input Sample", 0,0.5,0,0.5, -1,1, 0, 3);
  graph_inputenv.g_add_graph("Input Gain Envelope", 1,1,0.5,1, -graph_range,graph_range, _INF, 2);
  graph_inputenvmax.g_add_graph("Input Gain Max", 1,0.65,0,1, -graph_range,graph_range, _INF, 2);
  graph_input.g_add_graph("Input Gain", 0,0.7,1,1, -graph_range,graph_range, _INF, 2);
  graph_agc.g_add_graph("AGC", 1,0,0,1, -graph_range,graph_range, _INF, 0);

  PEAK_DEBUG ? graph_peak_debug.g_add_graph("Peak Debug", 0.4,0,0,1, -graph_range,graph_range, _INF, 2);
  PEAK_DEBUG ? graph_peak_debug2.g_add_graph("Output Debug", 0.4,0.5,0,1, -graph_range,graph_range, _INF, 2);
  use_peak_limiter ? graph_peak.g_add_graph("Peak Limiter", 0.5,0,0.5,1, -graph_range,graph_range, _INF, 2);
  agc_envelope_pgain = agc_envelope_gain;
  agc_output_pgain = agc_output_gain;          // Never used anyway

  unapplied_settings = 0;
  agc_by_orig = agc_by;
  agc_to_orig = agc_to;
  agc_lookahead_orig = agc_lookahead;
  agc_release_rate_orig = agc_release_rate;
  agc_min_orig = agc_min;
  agc_max_orig = agc_max;
  cfg_use_peak_limiter_orig = cfg_use_peak_limiter;
  use_pdc_delay_orig = use_pdc_delay;
);

Reset();

@slider

(
  (agc_by_orig != agc_by) ||
  //(agc_to_orig != agc_to) ||
  (agc_lookahead_orig != agc_lookahead) ||
  //(agc_release_rate_orig != agc_release_rate) ||
  //(agc_min_orig != agc_min) ||
  //(agc_max_orig != agc_max) ||
  (cfg_use_peak_limiter_orig != cfg_use_peak_limiter) ||
  (use_pdc_delay_orig != use_pdc_delay)
) ? (unapplied_settings = 1);

//Reset();  // this would reset on output automation

@block

want_reset =
  (num_ch > 0 && num_ch != last_nch) ||
  srate != last_sr;
last_nch=num_ch;
last_sr=srate;

cfg_reinit && (play_state&1) ? (
  abs(play_position-last_play_pos) > 0.5 ? want_reset=1;
  last_play_pos=play_position+samplesblock/srate;
);

want_reset ? Reset();

//pdc_delay = agc_lookahead_sz;

pk_decay=pow(0.5, samplesblock/srate/PEAK_METER_DECAY);

ch0.decay(); ch1.decay();
num_ch > 2 ? ( ch2.decay(); ch3.decay();
num_ch > 4 ? ( ch4.decay(); ch5.decay();
num_ch > 6 ? ( ch6.decay(); ch7.decay();
num_ch > 8 ? ( ch8.decay(); ch9.decay(); ch10.decay(); ch11.decay();
num_ch > 12 ? ( ch12.decay(); ch13.decay(); ch14.decay(); ch15.decay();
num_ch > 16 ? ( ch16.decay(); ch17.decay(); ch18.decay(); ch19.decay();
num_ch > 20 ? ( ch20.decay(); ch21.decay(); ch22.decay(); ch23.decay();
num_ch > 24 ? ( ch24.decay(); ch25.decay(); ch26.decay(); ch27.decay(); ch28.decay(); ch29.decay(); ch30.decay(); ch31.decay();
num_ch > 32 ? ( ch32.decay(); ch33.decay(); ch34.decay(); ch35.decay(); ch36.decay(); ch37.decay(); ch38.decay(); ch39.decay();
num_ch > 40 ? ( ch40.decay(); ch41.decay(); ch42.decay(); ch43.decay(); ch44.decay(); ch45.decay(); ch46.decay(); ch47.decay();
num_ch > 48 ? ( ch48.decay(); ch49.decay(); ch50.decay(); ch51.decay(); ch52.decay(); ch53.decay(); ch54.decay(); ch55.decay();
num_ch > 56 ? ( ch56.decay(); ch57.decay(); ch58.decay(); ch59.decay(); ch60.decay(); ch61.decay(); ch62.decay(); ch63.decay();
))))))))))));

function make_output_slider(b, base) ( cfg_out_auto>=2 && !(cfg_out_auto&1) ? base - b : b );
outparm_lufs_m = make_output_slider(lufs_m_db + (cfg_mono ? -3 : 0),-100);
outparm_lufs_s = make_output_slider(lufs_s_db + (cfg_mono ? -3 : 0),-100);
outparm_lufs_i = make_output_slider(lufs_i_db + (cfg_mono ? -3 : 0),-100);
outparm_lufs_lra = make_output_slider(lra_db_diff,100);
outparm_peak = make_output_slider(global_peak > 0 ? log(global_peak)*20/LOG10 : -150,-150);
outparm_rms_m = make_output_slider(rms_m_db + (cfg_mono ? -3 : 0),-100);
outparm_rms_i = make_output_slider(rms_i_db + (cfg_mono ? -3 : 0),-100);
outparm_agc_gain = agc_output_gain;
outparm_agc_winmax = agc_winmax;
outparm_agc_envelope = -agc_envelope_gain;

cfg_out_auto ? slider_automate(
  cfg_out_auto > 2 ? (2^(29 + floor((cfg_out_auto-3)/2))) : (
    2^29 | // (true) peak
    2^30 | // rms-m
    2^31 | // rms-i
    2^32 | // lufs-m
    2^33 | // lufs-s
    2^34 | // lufs-i
    2^35 | // lra
    2^36 | // agc gain
    2^37 | // agc window max
    2^38 | // agc envelope
    0
  )
);

@sample

global_sample_peak=rval=lval=0;

ch0.proc(spl0); ch1.proc(spl1);
num_ch > 2 ? ( ch2.proc(spl2); ch3.proc(spl3);
num_ch > 4 ? ( ch4.proc(spl4); ch5.proc(spl5);
num_ch > 6 ? ( ch6.proc(spl6); ch7.proc(spl7);
num_ch > 8 ? ( ch8.proc(spl8); ch9.proc(spl9); ch10.proc(spl10); ch11.proc(spl11);
num_ch > 12 ? ( ch12.proc(spl12); ch13.proc(spl13); ch14.proc(spl14); ch15.proc(spl15);
num_ch > 16 ? ( ch16.proc(spl16); ch17.proc(spl17); ch18.proc(spl18); ch19.proc(spl19);
num_ch > 20 ? ( ch20.proc(spl20); ch21.proc(spl21); ch22.proc(spl22); ch23.proc(spl23);
num_ch > 24 ? ( ch24.proc(spl24); ch25.proc(spl25); ch26.proc(spl26); ch27.proc(spl27); ch28.proc(spl28); ch29.proc(spl29); ch30.proc(spl30); ch31.proc(spl31);
num_ch > 32 ? ( ch32.proc(spl32); ch33.proc(spl33); ch34.proc(spl34); ch35.proc(spl35); ch36.proc(spl36); ch37.proc(spl37); ch38.proc(spl38); ch39.proc(spl39);
num_ch > 40 ? ( ch40.proc(spl40); ch41.proc(spl41); ch42.proc(spl42); ch43.proc(spl43); ch44.proc(spl44); ch45.proc(spl45); ch46.proc(spl46); ch47.proc(spl47);
num_ch > 48 ? ( ch48.proc(spl48); ch49.proc(spl49); ch50.proc(spl50); ch51.proc(spl51); ch52.proc(spl52); ch53.proc(spl53); ch54.proc(spl54); ch55.proc(spl55);
num_ch > 56 ? ( ch56.proc(spl56); ch57.proc(spl57); ch58.proc(spl58); ch59.proc(spl59); ch60.proc(spl60); ch61.proc(spl61); ch62.proc(spl62); ch63.proc(spl63);
))))))))))));

(win_pos += 1) >= win_len ? (
  win_pos=0;
  win_cnt += 1;

  prev_rval=rms_buf[cur_buf];
  rms_buf[cur_buf]=rval;
  
  prev_lval=lufs_buf[cur_buf];
  lufs_buf[cur_buf]=lval;
  
  prev_lval2=lufs_buf2[cur_buf2];
  lufs_buf2[cur_buf2]=lval;

  (cur_buf += 1) >= m_win_cnt ? cur_buf=0;
  (cur_buf2 += 1) >= s_win_cnt ? cur_buf2=0;

  rms_m_sum += (rval-prev_rval)*i_win_len;
  lufs_m_sum += (lval-prev_lval)*i_win_len;
  lufs_s_sum += (lval-prev_lval2)*s_win_len;
  
  WANT_INTEGRATED_ALWAYS || (play_state&1) ? (
    rms_i_sum += rms_m_sum;
    rms_i_sum_cnt += 1;
    rms_i_sum > 0 && rms_i_sum_cnt >= m_win_cnt ? (
      rms_i_db=log(rms_i_sum/rms_i_sum_cnt)*10/LOG10;
    ) : (
      rms_i_db=_INF;
    );
  );
  
  rms_m_sum > 0 && win_cnt >= m_win_cnt ? (
    rms_m_db=log(rms_m_sum)*10/LOG10;
    rms_m_db > rms_m_db_max ? rms_m_db_max=rms_m_db;
  ) : (
    rms_m_db=_INF;
  );
  
  lufs_m_sum > 0 && win_cnt >= m_win_cnt ? (
    lufs_m_sum > lufs_m_sum_max ? lufs_m_sum_max=lufs_m_sum;
    lufs_m_db=-0.691+log(lufs_m_sum)*10/LOG10;

    a = WANT_INTEGRATED_ALWAYS || (play_state&1) ? ((lufs_m_db+70)*BINS_PER_DB)|0 : -1;
    a >= 0 ? (
      a >= NUM_BINS ? a=NUM_BINS-1;
      lufs_a_sum += lufs_m_sum;
      lufs_a_sum_cnt += 1;
      lufs_a_hist[2*a] += 1;
      lufs_a_hist[2*a+1] += lufs_m_sum;
      db_hist_max = max(db_hist_max,db_hist[min(a*DB_PER_BIN,74)] += 1);

      lufs_a_db=-0.691+log(lufs_a_sum/lufs_a_sum_cnt)*10/LOG10;
      lufs_a_gate=((lufs_a_db-10+70)*BINS_PER_DB)|0;
      lufs_i_sum=0;
      lufs_i_cnt=0;
      bin=max(lufs_a_gate,0);
      loop(NUM_BINS-bin,
        lufs_i_cnt += lufs_a_hist[2*bin];
        lufs_i_sum += lufs_a_hist[2*bin+1];
        bin += 1;
      );
      lufs_i_db=lufs_i_sum > 0 ? -0.691+log(lufs_i_sum/lufs_i_cnt)*10/LOG10 : _INF;
    );
  ) : (
    lufs_m_db=_INF;
  );

  lufs_s_sum > 0 && win_cnt >= s_win_cnt ? (
    lufs_s_sum > lufs_s_sum_max ? lufs_s_sum_max=lufs_s_sum;
    lufs_s_db=-0.691+log(lufs_s_sum)*10/LOG10;

    b = WANT_INTEGRATED_ALWAYS || (play_state&1) ? ((lufs_s_db+70)*BINS_PER_DB)|0 : -1;
    b >= 0 ? (
      b >= NUM_BINS ? b=NUM_BINS-1;
      lufs_b_sum += lufs_s_sum;
      lufs_b_sum_cnt += 1;
      lufs_b_hist[b] += 1;

      lufs_b_db=-0.691+log(lufs_b_sum/lufs_b_sum_cnt)*10/LOG10;
      lufs_b_gate=((lufs_b_db-20+70)*BINS_PER_DB)|0;

      lra_cnt=0;
      bin=max(lufs_b_gate,0);
      loop(NUM_BINS-bin,
        lra_cnt += lufs_b_hist[bin];
        bin += 1;
      );
      lra_cnt >= 20 ? (
        lra_cnt_lo=lra_cnt_hi=0;
        bin=lufs_b_gate;
        while(bin < NUM_BINS && lra_cnt_lo < lra_cnt*0.10)
        (
          lra_cnt_lo += lufs_b_hist[bin];
          bin += 1;
        );
        bin_lo=bin-1;
        bin=NUM_BINS-1;
        while(bin >= lufs_b_gate && lra_cnt_hi < lra_cnt*0.05)
        (
          lra_cnt_hi += lufs_b_hist[bin];
          bin -= 1;
        );
        bin_hi=bin+1;
        lra_db_lo=bin_lo*DB_PER_BIN-70;
        lra_db_hi=bin_hi*DB_PER_BIN-70;
        lra_db_diff = lra_db_hi-lra_db_lo;
      );
    );
  ) : (
    lufs_s_db=_INF;
  );
);

(play_state&1) ? (
  spl0_buf.rb_push(spl0);
  spl1_buf.rb_push(spl1);
  global_sample_peak_db = (global_sample_peak > 0 ? log(global_sample_peak)*20/LOG10 : _INF);
  agc_input_prev = agc_input;
  agc_input =
    agc_by == 0 || agc_by == 1 ? global_sample_peak_db : // Peak, True Peak
    agc_by == 2 ? (rms_m_db + (cfg_mono ? -3 : 0)) : // RMS-M
    agc_by == 3 ? (rms_i_db + (cfg_mono ? -3 : 0)) : // RMS-I
    agc_by == 4 ? (lufs_m_db + (cfg_mono ? -3 : 0)) : // LUFS-M
    agc_by == 5 ? (lufs_s_db + (cfg_mono ? -3 : 0)) : // LUFS-S
    agc_by == 6 ? (lufs_i_db + (cfg_mono ? -3 : 0)) : // LUFS-I
    agc_by == 7 ? lra_db_diff : // LUFS-LRA
    0;
  agc_buf.maxwin_add(agc_input, agc_envelope_gain);
  agc_debug_dq_sz = agc_buf.dq.dq_size();

  agc_winmax = agc_buf.maxwin_max();
  agc_target_gain = agc_winmax;
  agc_target_off = agc_buf.maxwin_off();
  agc_envelope_gain = agc_input_prev > _INF ?
    mutate_gain_envelope(agc_envelope_gain, agc_target_gain, agc_target_off, agc_pre, agc_release_rate) :
    agc_target_gain;  // Initialize with the "target" on first appearance of maningful value
  agc_output_gain = min(max(-agc_envelope_gain + agc_to, agc_min), agc_max);

  g_nextsample();
  graph_smpl.g_sample((spl0+spl1)/2);
  graph_inputenvmax.g_sample(agc_winmax);
  graph_inputenv.g_sample(agc_envelope_gain);
  graph_input.g_sample(agc_input);

  use_peak_limiter ? (
    agc_pbuf_delay.rb_push(global_sample_peak_db);
    agc_pbuf.maxwin_add(agc_pbuf_delay.rb_get_tail(), agc_envelope_gain);  // use general envelope for this purpose

    agc_peak_off = agc_pbuf.maxwin_off();
    agc_peak_max = agc_pbuf.maxwin_max();
    engage_limiter = (agc_peak_max+agc_output_gain > 0);
    agc_envelope_pgain =
      mutate_gain_envelope(agc_envelope_pgain,
                         engage_limiter ? agc_peak_max : agc_envelope_gain,
                         agc_peak_off, agc_peak_pre, agc_peak_release_db_sec);
    agc_output_pgain = min(max(-agc_envelope_pgain, agc_min), agc_max);
    agc_or_peak_output_gain = min(agc_output_pgain, agc_output_gain);
    graph_peak.g_sample(agc_output_pgain < agc_output_gain ? -agc_output_pgain : _INF);
    //graph_peak.g_sample(-agc_output_pgain); // for debug
    //graph_peak.g_sample(global_sample_peak_db);
    //graph_peak.g_sample(agc_pbuf.maxwin_max());
    PEAK_DEBUG ? graph_peak_debug.g_sample(global_sample_peak_db);
  ) : (
    agc_or_peak_output_gain = agc_output_gain;
  );

  graph_agc.g_sample(agc_or_peak_output_gain);
  agc_spl_scale = 10^(agc_or_peak_output_gain/20);

  spl0 = spl0_buf.rb_get_tail() * agc_spl_scale;
  spl1 = spl1_buf.rb_get_tail() * agc_spl_scale;
  PEAK_DEBUG ? (debug_peak = max(abs(spl0), abs(spl1)); graph_peak_debug2.g_sample((debug_peak > 0 ? log(debug_peak)*20/LOG10 : _INF)); );
);

@gfx 800 320

ch0.copypks(); ch1.copypks();
num_ch > 2 ? ( ch2.copypks(); ch3.copypks();
num_ch > 4 ? ( ch4.copypks(); ch5.copypks();
num_ch > 6 ? ( ch6.copypks(); ch7.copypks();
num_ch > 8 ? ( ch8.copypks(); ch9.copypks(); ch10.copypks(); ch11.copypks();
num_ch > 12 ? ( ch12.copypks(); ch13.copypks(); ch14.copypks(); ch15.copypks();
num_ch > 16 ? ( ch16.copypks(); ch17.copypks(); ch18.copypks(); ch19.copypks();
num_ch > 20 ? ( ch20.copypks(); ch21.copypks(); ch22.copypks(); ch23.copypks();
num_ch > 24 ? ( ch24.copypks(); ch25.copypks(); ch26.copypks(); ch27.copypks(); ch28.copypks(); ch29.copypks(); ch30.copypks(); ch31.copypks();
num_ch > 32 ? ( ch32.copypks(); ch33.copypks(); ch34.copypks(); ch35.copypks(); ch36.copypks(); ch37.copypks(); ch38.copypks(); ch39.copypks();
num_ch > 40 ? ( ch40.copypks(); ch41.copypks(); ch42.copypks(); ch43.copypks(); ch44.copypks(); ch45.copypks(); ch46.copypks(); ch47.copypks();
num_ch > 48 ? ( ch48.copypks(); ch49.copypks(); ch50.copypks(); ch51.copypks(); ch52.copypks(); ch53.copypks(); ch54.copypks(); ch55.copypks();
num_ch > 56 ? ( ch56.copypks(); ch57.copypks(); ch58.copypks(); ch59.copypks(); ch60.copypks(); ch61.copypks(); ch62.copypks(); ch63.copypks();
))))))))))));

function set_color(trgb)
(
  gfx_set((trgb&$xff0000)/$xff0000,(trgb&$xff00)/$xff00,(trgb&$xff)/$xff);
);

gfx_clear=((BG_COLOR&$xff)<<16)|(BG_COLOR&$xff00)|((BG_COLOR&$xff0000)>>16);

fsz=FONT_SZ_MIN+min(gfx_w/100,FONT_SZ_MAX-FONT_SZ_MIN);
fsz *= 1.0;
gfx_ext_retina > 1 ? fsz *= 1.5;


gfx_setfont(1, "Arial", fsz*3, $'b');
set_color($x001018);  gfx_rect(gfx_w/2, 0, gfx_w, gfx_h);
set_color($x002040);  gfx_rect(gfx_w/2-1, 0, 3, gfx_h);
gfx_x = gfx_w/2; gfx_y = 0; set_color($x002050); gfx_drawstr("Lookahead",1|256,gfx_w,0);
//gfx_x = 0;       gfx_y = 0; set_color($x002040); gfx_drawstr("Delay",1|256,gfx_w/2,0);
gfx_setfont(1,"Arial",fsz*1.5,$'b');
gfx_x = 0; gfx_y = 0; set_color($xA00000); gfx_drawstr("Gain",1|256,gfx_w,0);
gfx_x = 0; gfx_y = gfx_texth; set_color($x80C0C0); gfx_setfont(1,"Arial",fsz*0.8,$'b'); gfx_drawstr("target gain",1|256,gfx_w,0);

gfx_setfont(1,"Arial",fsz);

yu=gfx_texth*7/8;
yt = (gfx_h-yu*3.5+yu)|0;
ylo=gfx_h-yu;

has_click = (mouse_cap&1) && !(last_cap&1);
last_cap=mouse_cap;

function gfx_bar(x1, y1, x2, y2) local(t) (
  x1 > x2 ? (t=x1;x1=x2;x2=t;);
  y1 > y2 ? (t=y1;y1=y2;y2=t;);
  gfx_rect(x1, y1, x2-x1+1, y2-y1+1);
);
function agc_y(val) ( graph_agc._g_y(val, graph_agc._g_k(), graph_agc.min); );
x=gfx_w/2;

y=0;
loop(10,
  graph_agc.g_const(y, 0.5,0.5,0.8,0.4);
  graph_agc.g_const(-y, 0.5,0.5,0.8,0.4);
  y += 10;
);
graph_smpl.g_paint();
graph_inputenvmax.g_paint2(-gfx_w/2);
graph_inputenv.g_paint2(-gfx_w/2);
graph_input.g_paint2();
graph_agc.g_paint2(-gfx_w/2);
use_peak_limiter ? graph_peak.g_paint2(-gfx_w/2);
PEAK_DEBUG ? graph_peak_debug.g_paint2();
PEAK_DEBUG ? graph_peak_debug2.g_paint2(-gfx_w/2);

set_color($xFF0000);
gfx_bar(x-20, agc_y(0), x+20, agc_y(agc_output_gain));
set_color($x80C0C0);
y = agc_y(-agc_target_gain + agc_to);
gfx_bar(x-4, agc_y(0), x+4, y);
gfx_a = 0.6;
gfx_line(x, y, (agc_buf.dq.dq_front() - agc_pre - agc_buf.buf.head + agc_lookahead_sz)/agc_lookahead_sz * gfx_w/2 + gfx_w/2, y);
//gfx_triangle(x-5, agc_y(agc_target_gain*0.7), x+5, agc_y(agc_target_gain*0.7), x, agc_y(agc_target_gain));
set_color($xFFFFFF);
gfx_setfont(1,"Arial",fsz, $'b');
gfx_x = x+20; gfx_y = agc_y(agc_output_gain) - gfx_texth/2;
agc_output_gain >= 0 ? gfx_drawstr("+");
gfx_drawnumber(agc_output_gain, 2);
gfx_drawstr("dB");

PEAK_DEBUG && use_peak_limiter ? (
  set_color($xff00ff);
  gfx_circle((agc_peak_off)/agc_lookahead_sz * gfx_w/2 + gfx_w/2, agc_y(agc_peak_max), 5);
  i = 0;
  loop(agc_pbuf.dq.dq_size(),
    x = (agc_pbuf.dq.dq_nfront(i) - agc_pbuf.buf.head + agc_peak_lookahead_sz)/agc_lookahead_sz * gfx_w/2 + gfx_w/2;
    y = agc_pbuf.buf.rb_get_at(agc_pbuf.dq.dq_nfront(i));
    gfx_circle(x, agc_y(y), 2);
    gfx_line(x, agc_y(y), x, agc_y(y + agc_output_gain));
    (y + agc_output_gain > 0) ? gfx_line3(x, agc_y(0), x, agc_y(y + agc_output_gain));
    i += 1;
  );
);

gfx_x = agc_target_off/agc_lookahead_sz * gfx_w/2 + gfx_w/2;
gfx_y = agc_y(agc_winmax) - gfx_texth;
gfx_drawnumber(agc_winmax, 2);

set_color($x006080);
n=0;
loop(agc_buf.dq.dq_size(),
  x = agc_buf.dq.dq_nfront(n);
  y = agc_buf.buf.rb_get_at(x);
  x = (x - agc_buf.buf.head + agc_lookahead_sz)/agc_lookahead_sz * gfx_w/2 + gfx_w/2;
  gfx_line(x, agc_y(y), x, gfx_h);
  n += 1;
);

(
  set_color(MONO_COLOR);
  tc1=(gfx_w-gfx_texth*27/16)|0;
  tc2=(gfx_w-gfx_texth*21/16)|0;
  tr=(gfx_texth*7/16)|0;
  cfg_mono ? gfx_mode = 1;
  gfx_circle(tc1,yt,tr,cfg_mono?1:0,0);
  gfx_circle(tc2,yt,tr,cfg_mono?1:0,0);
  cfg_mono ? (
    set_color(BG_COLOR);
    gfx_x=tc1-tr; gfx_y=yt-tr+1;
    gfx_drawstr("M",1|4|256,tc2+tr,yt+tr+1);
  );
  has_click && abs(mouse_x-(tc1+tc2)/2) <= tr*3/2+1 && abs(mouse_y-yt) <= tr+1 ? (
    cfg_mono=!cfg_mono;
    slider_automate(cfg_mono);
    has_click=0;
  );
);
(
  set_color(TEXT_COLOR);
  tx=gfx_w-gfx_texth*3/2;
  gfx_rect(gfx_x=tx-gfx_texth/2,gfx_y=ylo-gfx_texth/2,gfx_texth+1,gfx_texth+1,0);
  gfx_drawstr("=",1|4,gfx_x+gfx_texth+1,gfx_y+gfx_texth+1);
  has_click && abs(mouse_x-tx) < gfx_texth/2 && abs(mouse_y-ylo) < gfx_texth/2 ? (
    sliders_showing = slider_show(UI_SLIDER_MASK,-1) > 0;
    has_click=0;
  );
);
(
  gfx_measurestr("Reset", x, y);
  x = gfx_w - x;
  mouse_in = (mouse_x >= x && mouse_x < gfx_w && mouse_y >= 0 && mouse_y <= y);
  set_color(unapplied_settings ? (mouse_in ? $xFF8080 : $xFF0000) : (mouse_in ? $x80FF80 : $x008000));
  gfx_rect(x, 0, gfx_w, y);
  set_color($x000000);
  gfx_x = x; gfx_y = 0;
  gfx_drawstr("Reset");
  has_click && mouse_in ? (
    Reset();
    has_click = 0;
  );
);

graph_input.g_trackmouse(2, 1, "dB");

//has_click ? Reset();




/*
// Appreciate IXix: https://forum.cockos.com/showpost.php?p=364454

SHOWDEBUG = 0; //Set non-zero to enable the debug view

SHOWDEBUG ?
(    
    //Options
    dPrecision = 2;             //Decimal places for numbers
    dLeft = 300;                  //Debug window position in pixels
    dTop = 5;                   //Debug window position in pixels
    dRows = 14;                  //Row count for buffer data
    dCols = 3;                  //Column count for buffer data

    dRowHeight = 20;            //Row height offset for buffer data
    dColWidth = 140;             //Column width offset for buffer data
    dMarginX = 5;               //Left margin in pixels
    dMarginY = 5;               //Top margin in pixels
    dBufferY = 30;              //Vertical offset for buffer data display
    
    gfx_a = 1;
    gfx_mode = 0;

    //Calculate debug window size
    dWidth = dCols * dColWidth + dMarginX;
    dHeight = dRows * dRowHeight + dBufferY;

    //Set background colour
    gfx_r=1;gfx_g=1;gfx_b=1;gfx_a=1;

    //Erase
    gfx_x = dLeft;
    gfx_y = dTop;
    gfx_rectto(dLeft + dWidth, dTop + dHeight);

    //Set text colour
    gfx_r=.7;gfx_g=0;gfx_b=.3;

    //Draw debug vars
    gfx_x = dLeft + dMarginX; gfx_y = dTop + dMarginY;
    gfx_drawNumber(debug1, dPrecision);

    gfx_x = dLeft + dMarginX + dColWidth; gfx_y = dTop + dMarginY;
    gfx_drawNumber(debug2, dPrecision);

    gfx_x = dLeft + dMarginX + dColWidth * 2; gfx_y = dTop + dMarginY;
    gfx_drawNumber(debug3, dPrecision);

    gfx_x = dLeft + dMarginX + dColWidth * 3; gfx_y = dTop + dMarginY;
    gfx_drawNumber(debug4, dPrecision);

    gfx_x = dLeft + dMarginX + dColWidth * 4; gfx_y = dTop + dMarginY;
    gfx_drawNumber(debug5, dPrecision);

    gfx_x = dLeft + dMarginX + dColWidth * 5; gfx_y = dTop + dMarginY;
    gfx_drawNumber(debug6, dPrecision);

    //Draw separator
    gfx_x = dLeft + dMarginX;
    gfx_y = dTop + dBufferY - dMarginY;
    gfx_lineto(dLeft + dWidth - dMarginX, gfx_y, 1);

    //Draw buffer data
    di = 0;
    dRow = 0;
    loop
    (
        dRows,
        dCol = 0;
        loop
        (
            dCols,
            gfx_x = dLeft + dMarginX + dCol * dColWidth;
            gfx_y = dTop + dMarginY + dBufferY + dRow * dRowHeight;
            i = (di / 3)|0;
            what = di % 3;
            (i < agc_debug_dq_sz) && (
              (what == 0 && gfx_drawNumber(agc_buf.dq.dq_nfront(i), dPrecision);) ||
              (what == 1 && gfx_drawNumber(agc_buf.buf.rb_get_at(agc_buf.dq.dq_nfront(i)), dPrecision););
              (what == 2 && gfx_drawNumber(
                (agc_buf.buf.rb_get_at(agc_buf.dq.dq_nfront(i))-agc_envelope_gain)/(agc_buf.dq.dq_nfront(i)-agc_buf.buf.head+agc_lookahead_sz),
                dPrecision+5););
            );
            di += 1;
            dCol += 1;
        );
        dRow += 1;
    );

);
*/

