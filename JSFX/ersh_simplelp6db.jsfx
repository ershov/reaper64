/**
  JSFX Name: Simple 1-Pole Filter (phase corrected)
  Author: ersh, Yury Ershov
  Licence: GPL
  REAPER: 6.0
  Version: 2.0.0
  About:
    Improved version of Reaper's standard filter.
    Phase correction for High-Pass mode improves the filter's efficiency but may add clicks when the cutoff frequency changes.
    Added Rdy/Wet slider.
  Screenshot: https://i.imgur.com/6025IWy.png
  Link: https://github.com/ershov/reaper64
  Changelog:
    v1.0:
    * Initial release
    v2.0:
    * Add Phase correction for HP mode.
    * Add Wet/Dry balance.
    * Original waveform: https://i.imgur.com/35YRvDD.png Improved waveform: https://i.imgur.com/6025IWy.png
*/

// (C) 2008-2009, Lubomir I. Ivanov
// (C) 2021, Yury Ershov

// NO WARRANTY IS GRANTED. THIS PLUG-IN IS PROVIDED ON AN "AS IS" BASIS, WITHOUT
// WARRANTY OF ANY KIND. NO LIABILITY IS GRANTED, INCLUDING, BUT NOT LIMITED TO,
// ANY DIRECT OR INDIRECT,  SPECIAL,  INCIDENTAL OR CONSEQUENTIAL DAMAGE ARISING
// OUT OF  THE  USE  OR INABILITY  TO  USE  THIS PLUG-IN,  COMPUTER FAILTURE  OF
// MALFUNCTION INCLUDED.  THE USE OF THE SOURCE CODE,  EITHER  PARTIALLY  OR  IN
// TOTAL, IS ONLY GRANTED,  IF USED IN THE SENSE OF THE AUTHOR'S INTENTION,  AND
// USED WITH ACKNOWLEDGEMENT OF THE AUTHOR. FURTHERMORE IS THIS PLUG-IN A  THIRD
// PARTY CONTRIBUTION,  EVEN IF INCLUDED IN REAPER(TM),  COCKOS INCORPORATED  OR
// ITS AFFILIATES HAVE NOTHING TO DO WITH IT.  LAST BUT NOT LEAST, BY USING THIS
// PLUG-IN YOU RELINQUISH YOUR CLAIM TO SUE IT'S AUTHOR, AS WELL AS THE CLAIM TO
// ENTRUST SOMEBODY ELSE WITH DOING SO.
// 
// Released under GPL:
// <http://www.gnu.org/licenses/>.

//=================================================
desc: Simple 1-Pole Filter (phase corrected)
//tags: filter
//author: Liteon, ERSH

slider1:0<0,1,1{Stereo,Mono}>Processing
slider2:0<0,1,1{LP,HP}>Filter Type
slider3:100<0,100,0.05>Cutoff (Scale)
slider4:1<0,1,1{No,Yes}>Phase Correction (for HP)
slider5:100<0,100,0.1>Wet/Dry Balance %
slider6:0<-25,25,0.05>Output (dB)

in_pin:left input
in_pin:right input
out_pin:left output
out_pin:right output

@init

delay_max = 5000;

//=================================================
@slider
//mono
mono = slider1;
//type
hp = slider2;

//exp scale
sx = 16+slider3*1.20103;
cutoff = floor(exp(sx*log(1.059))*8.17742);

//coeff
cutoff = min(cutoff,20000);
lp_cut = 2*$pi*cutoff;
lp_n = 1/(lp_cut + 3*srate);
lp_b1 = (3*srate - lp_cut)*lp_n;
lp_a0 = lp_cut*lp_n;

//strength
wet = slider5/100;
dry = 1 - slider5/100;

//outgain
outgain = 10^(slider6/20);

//delay init
delay = min(floor(srate / cutoff / 5), delay_max);
pdc_delay = hp == 0 || (!slider4) ? 0 : delay;
memset(0, 0, delay*2);
delay_idx_write = 0;
delay_idx_read = 1;

//=================================================
@sample

function WetDry(splDry, splWet) ( splDry*dry + splWet*wet; );

delay_idx_write = delay_idx_read;
delay_idx_read = (delay_idx_read+1) % delay;

//stereo
mono == 0 ? (

//recursion
0[delay_idx_write*2] = inl = spl0;
1[delay_idx_write*2] = inr = spl1;
lp_outl = 2*inl*lp_a0 + lp_outl*lp_b1;
lp_outr = 2*inr*lp_a0 + lp_outr*lp_b1;

//type
hp == 0 ? (
  spl0 = WetDry(spl0, lp_outl*outgain);
  spl1 = WetDry(spl1, lp_outr*outgain);
) : (
  slider4 ? (
    spl0 = WetDry(0[delay_idx_read*2], (0[delay_idx_read*2]-lp_outl)*outgain);
    spl1 = WetDry(1[delay_idx_read*2], (1[delay_idx_read*2]-lp_outr)*outgain);
  ) : (
    spl0 = WetDry(spl0, (inl-lp_outl)*outgain);
    spl1 = WetDry(spl1, (inr-lp_outr)*outgain);
  );
);

) : (
//mono

//recursion
0[delay_idx_write*2] = inl = (spl0+spl1)/2;
lp_outl = 2*inl*lp_a0 + lp_outl*lp_b1;

//type
hp == 0 ? (
  spl0=spl1=WetDry(in1, lp_outl*outgain);
) : (
  slider4 ? (
    spl0=spl1=WetDry(0[delay_idx_read*2], (0[delay_idx_read*2]-lp_outl)*outgain);
  ) : (
    spl0=spl1=WetDry(inl, (inl-lp_outl)*outgain);
  );
);

);

//=================================================
@gfx 100 16

//draw freq scale numbers
gfx_x=gfx_y=5;
gfx_lineto(gfx_x, gfx_y,0);
gfx_r=gfx_b=0;
gfx_g=gfx_a=1;
gfx_drawchar($'F');
gfx_drawchar($' ');
gfx_drawchar($'=');
gfx_drawchar($' ');
gfx_drawnumber(cutoff,0);
gfx_drawchar($' ');
gfx_drawchar($'H');
gfx_drawchar($'z');

