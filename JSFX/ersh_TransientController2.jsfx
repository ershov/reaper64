/**
  JSFX Name: Transient Controller 2 by ERSH
  Author: ersh
  Licence: Apache 2.0
  REAPER: 6.0
  Version: 2.2.0
  About:
    Extended version of standard TransientController.
    Screenshot https://i.imgur.com/UMMDxQI.png
    Video https://i.imgur.com/UMMDxQI.png
  Screenshot: https://i.imgur.com/UMMDxQI.png
  Link: https://i.imgur.com/UMMDxQI.png
  Link: https://youtu.be/twKTY5g27fE
  Changelog:
    v2.0:
    * Initial release
    v2.1:
    * Add gain smoothness parameter.
    * Increased internal frame rate to 1500 for finer peak resolution.
    * Performance optimization.
    v2.2:
    * Fix handling of release rate.
    * Add a "Hold" parameter.
*/

// (C) 2007, Michael Gruhn.
// (C) 2021-2025, Yury Ershov.

// NO WARRANTY IS GRANTED. THIS PLUG-IN IS PROVIDED ON AN "AS IS" BASIS, WITHOUT
// WARRANTY OF ANY KIND. NO LIABILITY IS GRANTED, INCLUDING, BUT NOT LIMITED TO,
// ANY DIRECT OR INDIRECT,  SPECIAL,  INCIDENTAL OR CONSEQUENTIAL DAMAGE ARISING
// OUT OF  THE  USE  OR INABILITY  TO  USE  THIS PLUG-IN,  COMPUTER FAILTURE  OF
// MALFUNCTION INCLUDED.  THE USE OF THE SOURCE CODE,  EITHER  PARTIALLY  OR  IN
// TOTAL, IS ONLY GRANTED,  IF USED IN THE SENSE OF THE AUTHOR'S INTENTION,  AND
// USED WITH ACKNOWLEDGEMENT OF THE AUTHOR. FURTHERMORE IS THIS PLUG-IN A  THIRD
// PARTY CONTRIBUTION,  EVEN IF INCLUDED IN REAPER(TM),  COCKOS INCORPORATED  OR
// ITS AFFILIATES HAVE NOTHING TO DO WITH IT.  LAST BUT NOT LEAST, BY USING THIS
// PLUG-IN YOU RELINQUISH YOUR CLAIM TO SUE IT'S AUTHOR, AS WELL AS THE CLAIM TO
// ENTRUST SOMEBODY ELSE WITH DOING SO.

// Further more is this released under the GPL License:
// http://www.gnu.org/licenses/gpl.html

desc:Transient Controller 2
//tags: processing dynamics transient
//author: LOSER, ERSH

slider1:sl_attack_pct=0<-100,100,.1>Attack (%)
slider2:sl_sustain_pct=0<-100,100,.1>Sustain (%)
slider3:sl_output=0<-12,12,.1>Output (dB)
slider4:sl_decay_detector=-30<-1000,-.1,.1>Transient Detector decay
slider5:sl_decay_attack=-1250<-10000,-.1,.1>Attack Detector decay
slider6:sl_decay_sustain=-3<-100,-.1,.1>Sustain Detector decay
slider7:sl_release_slow=0<0,1,1{No,Yes}>Use slow gain release
slider8:sl_release_rate=25<1,1000,.1>Gain Release Rate (dB/sec)
slider9:sl_hold=0<0,100,.1>Hold (ms)

in_pin:left input
in_pin:right input
out_pin:left output
out_pin:right output

@init

INF=150;
_INF=-INF;
LOG10 = log(10);

envDetector_b1 = exp(sl_decay_detector / srate );
envDetector_a0 = 1.0 - envDetector_b1;
envAttack_b1 = exp(sl_decay_attack / srate );
envAttack_a0 = 1.0 - envAttack_b1;
envSustain_b1 = exp(sl_decay_sustain / srate );
envSustain_a0 = 1.0 - envSustain_b1;
env_detector = env_attack = env_sustain = 0;
gain = 0;

g_maxx = 2000;         // number of points on axis X
g_framerate = 1500;    // graph frame rate
g_n = 4;               // number of graphs
memset(0, 0, g_maxx*g_n);
g_idx = 0;

//freembuf(g_maxx*g_n + 1);

g_spf = srate / g_framerate;  // samples per frame
g_sample = 0;
hold_attack = hold_decay = sample = 0;

__v2db = 20/LOG10;
function v2db(v) ( log(v)*__v2db );
function db2v(db) ( exp(db/__v2db) );  // = 2^(db/6) =  10 ^ (db/20)

@slider
//attack=sl_attack_pct/100;
attack_abs = abs(sl_attack_pct/100);
attack_sign = sl_attack_pct > 0 ? 1 : sl_attack_pct < 0 ? -1 : 0;
//sustain=sl_sustain_pct/50;
sustain_abs = abs(sl_sustain_pct/100);
sustain_sign = sl_sustain_pct > 0 ? 1 : sl_sustain_pct < 0 ? -1 : 0;
vol = db2v(sl_output);

envDetector_b1 = exp(sl_decay_detector / srate );
envDetector_a0 = 1.0 - envDetector_b1;
envAttack_b1 = exp(sl_decay_attack / srate );
envAttack_a0 = 1.0 - envAttack_b1;
envSustain_b1 = exp(sl_decay_sustain / srate );
envSustain_a0 = 1.0 - envSustain_b1;

gain_decay = sl_release_rate / srate;
hold_smpl = sl_hold * srate / 1000;

@sample
(play_state == 1 || play_state == 5) ? sample+=1;

maxSpl = max(abs(spl0),abs(spl1));
//env_detector = sqrt(env_detector_tmp = envDetector_a0*maxSpl + envDetector_b1*env_detector_tmp);
//env_attack = sqrt(env_attack_tmp = envAttack_a0*maxSpl + envAttack_b1*env_attack_tmp);
//env_sustain = sqrt(env_sustain_tmp = envSustain_a0*maxSpl + envSustain_b1*env_sustain_tmp);
env_detector = envDetector_a0*maxSpl + envDetector_b1*env_detector;
env_attack = envAttack_a0*maxSpl + envAttack_b1*env_attack;
env_sustain = envSustain_a0*maxSpl + envSustain_b1*env_sustain;

gainAttackLast = gainAttack;
gainSustainLast = gainSustain;

gainAttack  = log(max(env_attack/env_detector,1))*attack_abs;
gainSustain = log(max(env_sustain/env_detector,1))*sustain_abs;

hold_smpl > 0 ? (
  env_attack  > env_detector ? (hold_attack  = 0; gainAttackLast  = gainAttack  = max(gainAttack,  gainAttackLast))  : (hold_attack+=1)  < hold_smpl ? gainAttack  = gainAttackLast;
  env_sustain > env_detector ? (hold_sustain = 0; gainSustainLast = gainSustain = max(gainSustain, gainSustainLast)) : (hold_sustain+=1) < hold_smpl ? gainSustain = gainSustainLast;
) : (
  hold_attack = hold_sustain = 1;
);

sl_release_slow ? (
  hold_attack  >= hold_smpl ? (gainAttack  = max(max(gainAttack,  gainAttackLast  - gain_decay), 0));
  hold_sustain >= hold_smpl ? (gainSustain = max(max(gainSustain, gainSustainLast - gain_decay), 0));
);

gain = gainAttack*attack_sign + gainSustain*sustain_sign;

gainV = exp(gain) * vol;

spl0 *= gainV;
spl1 *= gainV;

play_state == 1 || play_state == 5 ? (g_sample = (g_sample + 1) % g_spf) == 0 ? (
  0[g_idx] = env_detector*5;
  1[g_idx] = env_attack*5;
  2[g_idx] = env_sustain*5;
  3[g_idx] = gain/5 + 0.5;
  g_idx = (g_idx + g_n) % (g_maxx*g_n);
);

@gfx 600 300

function g_y(y) (
  gfx_h - gfx_h * y;
);

function graph(idx, r, g, b, legend) (
  gfx_r = r; gfx_g = g; gfx_b = b; gfx_a = 1;

  gfx_x = 34; gfx_y = 4 + idx * (gfx_texth+4);
  gfx_drawstr(legend);
  gfx_y += 3;
  gfx_line(2, gfx_y, 30, gfx_y);

  i = 0;
  ii = g_idx;
  gfx_x = 0;
  gfx_y = idx[ii];
  while (
    x = i*gfx_w/g_maxx;
    gfx_a = (idx == 3) || (idx != 0 && idx[ii] > 0[ii]) ? 1 : 0.5;
    gfx_lineto(x, g_y(idx[ii]));
    idx != 0 && idx != 3 && idx[ii] > 0[ii] ? gfx_line(x, g_y(0[ii]), x, g_y(idx[ii]));
    i += 1;
    ii = (ii + g_n) % (g_maxx*g_n);
    i < g_maxx;
  );
);

gfx_clear = 0;
graph(2, 0, 0, 1, "Sustain (when exceeds Transient)");
graph(1, 1, 0, 0, "Attack (when exceeds Transient)");
graph(0, 1, 1, 1, "Transient");
graph(3, 0, 1, 0, "Gain");

mouse_x >= 0 && mouse_x < gfx_w && mouse_y >= 0 && mouse_y < gfx_h ? (
  val = 3[(floor(mouse_x*g_maxx/gfx_w)*g_n + g_idx) % (g_maxx*g_n)];
  gfx_circle(mouse_x, g_y(val), 2, 1, 1);

  val = (val-0.5)*5;
  gfx_y = 4; gfx_x = gfx_w*3/4;
  val >= 0 ? gfx_drawstr("+");
  gfx_drawnumber(val, 2); gfx_drawstr("dB");
);



